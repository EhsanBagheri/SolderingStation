***********************************************************
*
* MMBT3906
*
* Nexperia
*
* Switching PNP Transistor
* IC   = 200 mA
* VCEO = 40 V 
* hFE  = 100 - 300 @ 1V/10mA
*
*
*
*
* Package pinning does not match Spice model pinning.
* Package: SOT 23
* 
* Package Pin 1: Base
* Package Pin 2: Emitter
* Package Pin 3: Collector
*
*
* Extraction date (week/year): 09/2021
* Spice model includes temperature dependency.
*
**********************************************************
*#
* Transistor Q2, Resistor RQ and Diode D are dedicated to improve modeling in quasi
* saturation area and reverse area of operation. They do not reflect physical devices.
*
.SUBCKT MMBT3906 1 2 3
Q1 1 2 3 MAIN 0.9566
Q2 11 2 3 MAIN 0.04337
RQ 1 11 395.3
D1 1 2 DIODE
*
.MODEL MAIN PNP
+ IS = 4.935E-15
+ NF = 0.9674
+ ISE = 3.963E-16
+ NE = 1.316
+ BF = 196
+ IKF = 0.1333
+ VAF = 31.49
+ NR = 0.9623
+ ISC = 1.109E-15
+ NC = 1.025
+ BR = 0.75
+ IKR = 0.02381
+ VAR = 15.48
+ RB = 14
+ IRB = 0.00079
+ RBM = 0.01
+ RE = 0.3429
+ RC = 0.5432
+ XTB = 1.15
+ EG = 1.11
+ XTI = 0.8891
+ CJE = 1.06E-11
+ VJE = 0.6561
+ MJE = 0.3588
+ TF = 4.804E-10
+ XTF = 2
+ VTF = 2.287
+ ITF = 0.2042
+ PTF = 0
+ CJC = 6.397E-12
+ VJC = 0.5547
+ MJC = 0.39
+ XCJC = 1
+ TR = 1.7E-07
+ CJS = 0
+ VJS = 0.75
+ MJS = 0.333
+ FC = 0.751
.MODEL DIODE D
+ IS = 1E-18
+ N = 2.2
+ BV = 1000
+ IBV = 0.001
+ RS = 10
+ CJO = 0
+ VJ = 1
+ M = 0.5
+ FC = 0
+ TT = 0
+ EG = 1.11
+ XTI = 3
.ENDS